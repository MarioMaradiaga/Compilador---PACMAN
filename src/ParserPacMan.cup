// CUP specification for a simple expression evaluator (no actions)

import java_cup.runtime.*;

parser code {: 
public void syntax_error(java_cup.runtime.Symbol current) {
	int linea = ((MiToken) current.value).getLinea();
	int columna = ((MiToken) current.value).getColumna();
	String error = "";
	if (current.sym == sym.EOF) 
	{
		error = "Fin de archivo inesperado.";
	} else 
	{
		error = "Error de Sintaxis: No se esperaba el token (\"" + ( (MiToken)(current.value) ).getValor() + "\"), Linea: " + 
		(linea+1) + ", Columna: " + (columna);
	}
	System.out.println(error);
}

public void report_fatal_error(String message, Object info) 
{
    System.out.println("Se encontró un error fatal. No se puede continuar el parseo.");
}
:}

terminal INT, BOOL, FLOAT, CHAR, STRING, FOR, WHILE, IF, SWITCH, CASE, ASIG, MULT, SUMA, RESTA, DIV, PUNTOCOMA, COMA, PARIZQ; 
terminal PARDER, BRAIZQ, BRADER, MENOR, MAYOR, MENORIGUAL, MAYORIGUAL, IGUAL, NOIGUAL, DOSPUNTOS, BREAK, IDENT, ELSE, TRUE;
terminal FALSE, NULL, FUNCTION, VOID, RETURN, CADENA, AND, OR, NOT, ENTERO, CHARVAL, FLOATVAL, DEFAULT, MAIN, READ, WRITE;

non terminal s0, s1, asignacion, expArit, expBool, termino, factor, tipo, variosIdent, boolVal, declaracion, valor, num;
non terminal opRel, termBool, bloqueIf, bloqueElse, text, bloqueWhile, bloqueFor, declaracionValor;
non terminal bloqueSwitch, finCasos, casos, comienzo, bloqueFunction, tipoFunction, parametro, parametros, comparacion; 
non terminal retorno, valoresCaso, inOut, inOutVal, bloque, funcPar, funcDec, asignacionFor, s2, factorBool, primaryBool;

comienzo ::= MAIN BRAIZQ s0 BRADER
; 

s0 ::= 
	 | s1 s0
	 | s2 s0
;

s1 ::= declaracion
	 | asignacion
	 | inOut
	 | retorno
	 | error DOSPUNTOS
	 | error BRADER
;

s2 ::= bloqueIf
	 | bloqueWhile
	 | bloqueFor
	 | bloqueSwitch
	 | bloqueFunction
;


/*Tipos*/	
tipo ::= STRING
       | INT
 	   | CHAR
	   | FLOAT
       | BOOL
;

/*Declaraciones*/
declaracion ::= tipo variosIdent DOSPUNTOS
			  | declaracionValor
;

declaracionValor ::= tipo asignacion
;

variosIdent ::= IDENT COMA variosIdent
			  | IDENT
;

inOut ::= READ PARIZQ inOutVal PARDER DOSPUNTOS
		| WRITE PARIZQ inOutVal PARDER DOSPUNTOS
;

inOutVal ::= valor
		   | IDENT
;

retorno ::= RETURN valor DOSPUNTOS
		  | RETURN IDENT DOSPUNTOS
;

/*Valores*/
boolVal ::= TRUE
		  | FALSE
;
	
num ::= ENTERO 
	  | FLOATVAL
;

text ::= CADENA
		| CHARVAL
;

valor ::= num
		| text
		| boolVal
		| NULL
;


/*Asignaciones*/		
asignacion ::= variosIdent ASIG expBool DOSPUNTOS
;

asignacionFor ::= variosIdent ASIG expArit 
;

/*Expresiones Aritméticas*/
expArit ::= expArit SUMA termino
	| expArit RESTA termino
	| termino
;

termino ::= termino MULT factor
	| termino DIV factor
	| factor
;
	
factor ::= PARIZQ expBool PARDER
		 | valor
	     | IDENT
;

/*Operadores Relacionales*/
opRel ::= MENOR
		| MAYOR
		| MENORIGUAL
		| MAYORIGUAL
		| IGUAL
		| NOIGUAL
;

/*Expresion Booleana*/
expBool ::= termBool 
		  | expBool OR termBool
;

termBool ::= factorBool
		   | termBool AND factorBool
;

factorBool ::= primaryBool
			 | NOT primaryBool
;

primaryBool ::= comparacion
			  | expArit
;

comparacion ::= IDENT opRel expArit
;

/*Estructuras*/
bloqueIf ::= IF PARIZQ expBool PARDER BRAIZQ s0 BRADER bloqueElse
;

bloqueElse ::= ELSE bloqueIf
			 | ELSE bloque
			 | 
;

bloqueWhile ::= WHILE PARIZQ expBool PARDER bloque
			| WHILE error PARDER bloque
			| WHILE error bloque
;

bloqueFor ::= FOR PARIZQ declaracionValor expBool DOSPUNTOS asignacionFor PARDER bloque
			| FOR error PARDER bloque
			| FOR error bloque
;

bloqueSwitch ::= SWITCH PARIZQ IDENT PARDER BRAIZQ casos finCasos BRADER
;

valoresCaso ::= ENTERO
			  | CHARVAL
;

casos ::= CASE valoresCaso PUNTOCOMA s0 BREAK DOSPUNTOS casos
		| error valoresCaso PUNTOCOMA s0 BREAK DOSPUNTOS casos
		| error PUNTOCOMA s0 BREAK DOSPUNTOS casos
		| error s0 BREAK DOSPUNTOS casos
		| error DOSPUNTOS casos
		| CASE valoresCaso PUNTOCOMA s0 BREAK DOSPUNTOS
		| error valoresCaso PUNTOCOMA s0 BREAK DOSPUNTOS 
		| error PUNTOCOMA s0 BREAK DOSPUNTOS 
		| error s0 BREAK DOSPUNTOS 
		| error DOSPUNTOS 

;

finCasos ::= DEFAULT PUNTOCOMA s0 BREAK DOSPUNTOS
		   | DEFAULT error BREAK DOSPUNTOS
		   | 
;

tipoFunction ::= tipo
			   | VOID
;

parametro ::= tipo IDENT 
;

parametros ::= parametro COMA parametros 
			 | parametro 
;

bloqueFunction ::= funcDec funcPar bloque
;

funcDec ::= FUNCTION tipoFunction IDENT 
;

funcPar ::= PARIZQ parametros PARDER
;

bloque ::= BRAIZQ s0 BRADER 
		 | error s0 BRADER
;